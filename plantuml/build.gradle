group = "plantuml"
version = "1.0.0-SNAPSHOT"

ext {
	xtextVersion = "2.18.0"
}

repositories {
	jcenter()
	mavenCentral()
	google()
	gradlePluginPortal()
}

apply plugin: "java"
apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "org.xtext.xtend"
apply from: "${projectDir}/source-layout.gradle"

dependencies {
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
	testImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:${xtextVersion}"
	testImplementation "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:${xtextVersion}"
	api "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
	implementation "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
	implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
}

configurations {
	mwe2 {
		extendsFrom implementation
	}
	all {
		exclude group: "asm"
	}
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
	mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:2.11.0"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
	mwe2 "org.eclipse.xtext:xtext-antlr-generator:2.1.1"
}

task generateXtextLanguage(type: JavaExec) {
	main = "org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher"
	classpath = configurations.mwe2
	inputs.file "src/main/java/plantuml/GeneratePumlLanguage.mwe2"
	inputs.file "src/main/java/plantuml/PumlLanguage.xtext"
	outputs.dir "src/main/xtext-gen"
	args += "src/main/java/plantuml/GeneratePumlLanguage.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

test {
	useJUnitPlatform()

	afterTest { desc, result ->
		logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]
